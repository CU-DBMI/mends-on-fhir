package main
option "experiment/merge_modes"

// 2022-03-22: Removed Visit_Datetime. Replaced by ReformatDatetime() in datatypes.wstl
// 2022-03-22: Moved MendsMetaData to datatypes.wstl
// 2022-08-22: Moved Convert() to this file

def HasValue(v) {
  if isNotNil(v) and v != " " and toLower(v) != "none" and v != "" then {
    $this: true
  } else {
    $this: false
  }
}

def ConceptHasValue(c) {
  if HasValue(c) and c != "0"  then {
    $this: true
  } else {
    $this: false
  }
}

// *******************************************************************
//
// Convert_Value(value_to_convert: str, source_unit: str, target_unit: str)
// A simplified version of the Whistle $HarmonizeUnit() function
// Should be replaced with above in a refactor
//
// *******************************************************************


def Convert(v,lo,hi,from,to) {
    if (HasValue(v))  then {
        var convert.value: Convert_Value(v,from,to)
        if (HasValue(lo) )  then {
            var convert.low: Convert_Value(lo,from,to)
        }
        if (HasValue(hi) )  then {
            var convert.high: Convert_Value(hi,from,to)
        }
        $this: convert
    }
}


def Convert_Value(v,from, to) {
    var lfrom: toLower(from);
    var lto: toLower(to);

    if eq(lfrom, lto)  then {
        // No conversion required
        $this: tryParseNum(v)
    }
    if (matchesRegex(lfrom,"far") and matchesRegex(lto, "cel"))  then {
        // Farhenheit --> Celsius
        $this: (tryParseNum(v) - 32) * (5/9)
    }
    if matchesRegex(lfrom,"lb|pound") and matchesRegex(lto,"kg")  then {
        // Pounds --> kgs
        $this: tryParseNum(v) * 0.45359
    }
    if matchesRegex(lfrom,"oz|ounces") and matchesRegex(lto,"kg")  then {
        // Ounces --> kgs
        $this: tryParseNum(v) * 0.02834952
    }
    if matchesRegex(lfrom,"inch") and matchesRegex(lto,"cm")  then {
        // Inches --> Centimeters
        $this: tryParseNum(v) * 2.54
    }
}

def $Trim(str_to_trim) {
  // Pattern [!-~] matches all printable characters except space
  // Pattern [ -~] matches all printable characters including space
  // The second regex matches a single non-space printable character (eg "a")
  // From https://catonmat.net/my-favorite-regex

  var pattern: "[!-~][ -~]*[!-~]|[!-~]"
  var out: extractRegex(str_to_trim, pattern,$.0)
  
  $this: out[0]
}

// $Merge() is MENDS replacement for Whistle1 $Flatten() function
def $Merge(obj) {
  merge var result : obj[] ;
  $this: result
}