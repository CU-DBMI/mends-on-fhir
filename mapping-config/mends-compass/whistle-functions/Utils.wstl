// 2022-03-22: Removed Visit_Datetime. Replaced by ReformatDatetime() in datatypes.wstl
// 2022-03-22: Moved MendsMetaData to datatypes.wstl
// 2022-08-22: Moved Convert() to this file

def HasValue(v) {
  if $And($IsNotNil(v),v ~= " ", $ToLower(v)~= "none", v~="")  {
    $this: true
  } else {
    $this: false
  }
}

def ConceptHasValue(c) {
  if ($And(HasValue(c), c ~= "0") ) {
    $this: true
  } else {
    $this: false
  }
}

// *******************************************************************
//
// Convert_Value(value_to_convert: str, source_unit: str, target_unit: str)
// A simplified version of the Whistle $HarmonizeUnit() function
// Should be replaced with above in a refactor
//
// *******************************************************************


def Convert(v,lo,hi,from,to) {
    if (HasValue(v)) {
        convert.value: Convert_Value(v,from,to)
        if (HasValue(lo) ) {
            convert.low: Convert_Value(lo,from,to)
        }
        if (HasValue(hi) ) {
            convert.high: Convert_Value(hi,from,to)
        }
        $this: convert
    }
}


def Convert_Value(v,from, to) {
    var lfrom: $ToLower(from);
    var lto: $ToLower(to);

    if $Eq(lfrom, lto) {
        // No conversion required
        $this: $ParseFloat(v)
    }
    if ($MatchesRegex(lfrom,"far") and $MatchesRegex(lto, "cel")) {
        // Farhenheit --> Celsius
        $this: ($ParseFloat(v) - 32) * (5/9)
    }
    if $MatchesRegex(lfrom,"lb|pound") and $MatchesRegex(lto,"kg") {
        // Pounds --> kgs
        $this: $ParseFloat(v) * 0.45359
    }
    if $MatchesRegex(lfrom,"oz|ounces") and $MatchesRegex(lto,"kg") {
        // Ounces --> kgs
        $this: $ParseFloat(v) * 0.02834952
    }
    if $MatchesRegex(lfrom,"inch") and $MatchesRegex(lto,"cm") {
        // Inches --> Centimeters
        $this: $ParseFloat(v) * 2.54
    }
}