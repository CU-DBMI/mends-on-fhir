def Observation_Smoking(Observation, required context) {

  var data: Build_Data(Observation)

  // Observation Resource
  resourceType: "Observation"

  // id, 0..1, id
  id: data.id

  meta.profile[]: "http://hl7.org/fhir/us/core/StructureDefinition/us-core-smokingstatus"

  identifier[]: Identifier(Identifier_System("omop-id"), data.id)

  status : "final"

  category[]: data.category

  code: data.code

  subject : Reference("Patient", Observation.person_id);

  if context.config.output.phi = "yes" {
    effectiveDateTime: ReformatDatetime(Observation.observation_date)
  } else {
    effectiveDateTime: ReformatDatetime("2030-01-01")
  }
  

  // Top of the SNOMED hierarchy is at: https://browser.ihtsdotools.org/?perspective=full&conceptId1=365980008
  valueCodeableConcept:  data.valueCodeableConceptNew
}

def Build_Data(Observation) {
  id: $StrJoin("-", Observation.person_id, Observation.observation_date)

  category.coding[0].system: "http://terminology.hl7.org/CodeSystem/observation-category"
  category.coding[0].code: "social-history"
  // Shahim: not really needed
  //category.coding[0].display: "Social History"

  code.coding[0].system: "http://loinc.org"
  code.coding[0].code: "72166-2"
  code.coding[0].display: "Tobacco smoking status"


  
  valueCodeableConcept: CodeableConceptMapNull(Observation.panel_string, "ObsSmoking.panel-string--Observation.value", "" )

  valueCodeableConceptNew: GetCCNew(Observation);
  
}

def GetCCNew(Observation){
  $this: CodeableConcept([], Observation.panel)

  // see https://docs.google.com/spreadsheets/d/15JwgQPyoeNvFTfv7JsuVFvjpNSkrtLFVL9Za6YUhOOo/edit#gid=485331358

  if $MatchesRegex(Observation.panel, "\\(TobaccoUse:Not Asked\\)") {
    coding[]: Coding("http://snomed.info/sct", "", "266927001", "Tobacco smoking consumption unknown (finding)")
  }
  
  if $MatchesRegex(Observation.panel, "\\(TobaccoUse:Never\\)") {
    coding[]: Coding("http://snomed.info/sct", "", "266919005", "Never smoked tobacco (finding)")
  }

  if $MatchesRegex(Observation.panel, "\\(TobaccoUse:Quit\\)") {
    coding[]: Coding("http://snomed.info/sct", "", "8517006", "Ex-smoker (finding)")

  }

  if $MatchesRegex(Observation.panel, "\\(TobaccoUse:Yes\\)") {
    coding[]: Coding("http://snomed.info/sct", "", "77176002", "Smoker (finding)")
    
    // add a second more specific code. Jeff will only pick the first one anyway.
    coding[]: GetHeaviness(Observation)
  }
}

def GetHeaviness(Observation){

  if $MatchesRegex(Observation.panel, "\\(CigarettePacksPerDay:<1\\)") {
    var codings[]: Coding("http://snomed.info/sct", "", "428041000124106", "Occasional tobacco smoker (finding)")
  }

  if $MatchesRegex(Observation.panel, "\\(CigarettePacksPerDay:<10\\)") {
    var codings[]: Coding("http://snomed.info/sct", "", "428061000124105", "Light tobacco smoker (finding)")
  }

  if $MatchesRegex(Observation.panel, "\\(CigarettePacksPerDay:<20\\)") {
    var codings[]: Coding("http://snomed.info/sct", "", "56578002", "Moderate smoker (20 or less per day) (finding)")
  }

  if $MatchesRegex(Observation.panel, "\\(CigarettePacksPerDay:<30\\)|\\(CigarettePacksPerDay:<40\\)") {
    var codings[]: Coding("http://snomed.info/sct", "", "428071000124103", "Heavy tobacco smoker (finding)")
  }

  if $MatchesRegex(Observation.panel, "\\(CigarettePacksPerDay:>=40\\)") {
    var codings[]: Coding("http://snomed.info/sct", "", "230064005", "Very heavy cigarette smoker (finding)")
  }
  
  $this: codings
}